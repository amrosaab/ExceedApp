plugins {
    id 'com.android.application'
    id 'com.google.gms.google-services'
    id 'kotlin-android'
    id 'dev.flutter.flutter-gradle-plugin'
}

def localProperties = new Properties()
def localPropertiesFile = rootProject.file('local.properties')
if (localPropertiesFile.exists()) {
    localPropertiesFile.withReader('UTF-8') { reader ->
        localProperties.load(reader)
    }
}

def flutterRoot = localProperties.getProperty('flutter.sdk')
if (flutterRoot == null) {
    throw new GradleException("Flutter SDK not found. Define location with flutter.sdk in the local.properties file.")
}

def flutterVersionCode = localProperties.getProperty('flutter.versionCode')
if (flutterVersionCode == null) {
    flutterVersionCode = '1'
}

def flutterVersionName = localProperties.getProperty('flutter.versionName')
if (flutterVersionName == null) {
    flutterVersionName = '1.0'
}

def envProperties = new Properties()
def envPropsFile = rootProject.file('../configs/env.props')
def envPropertiesFile = rootProject.file('../configs/env.properties')

if (envPropertiesFile.exists() && envPropsFile.exists()) {
    println "====================================================================="
    println "âš ï¸  Warning: env.properties is deprecated, please rename to env.props"
    println "====================================================================="
}

if (envPropertiesFile.exists() && !envPropsFile.exists()) {
    println "================================================================="
    println "âš ï¸  Warning: env.properties is deprecated and should not be used"
    println "ðŸª„ï¸  env.properties has been renamed to env.props automatically"
    println "================================================================="

    ant.move file: '../../configs/env.properties', tofile: '../../configs/env.props'
}

envPropsFile = rootProject.file('../configs/env.props')
envPropertiesFile = rootProject.file('../configs/env.properties')

if (envPropsFile.exists()) {
    println "ðŸ”§ Loading configs from configs/env.props..."
    envPropsFile.withReader('UTF-8') { reader ->
        envProperties.load(reader)
    }
} else {
    if (envPropertiesFile.exists()) {
        println "ðŸ”§ Loading configs from configs/env.properties..."
        envPropertiesFile.withReader('UTF-8') { reader ->
            envProperties.load(reader)
        }
    }
}

android {
    compileSdkVersion 35
    namespace  'com.exceedtoystore.app'

    ndkVersion "25.1.8937393"

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }
    lintOptions {
        disable 'InvalidPackage'
        checkReleaseBuilds false
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
        coreLibraryDesugaringEnabled true

    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    tasks.withType(JavaCompile) {
        options.warnings = false
    }

    defaultConfig {
        applicationId envProperties['androidPackageName']
        minSdkVersion 24
        targetSdkVersion 35
        versionCode 12
        versionName '1.0.12'
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        multiDexEnabled true
        resConfigs "en"

        manifestPlaceholders += [
                envatoPurchaseCode: envProperties['envatoPurchaseCode'],
                websiteUrl: envProperties['websiteUrl'],
                websiteDomain: envProperties['websiteDomain'],
                customScheme: envProperties['customScheme'],
                googleApiKeyAndroid: envProperties['googleApiKeyAndroid'],
                adMobAppIdAndroid: envProperties['adMobAppIdAndroid'],
                facebookClientToken: envProperties['facebookClientToken'],
                facebookLoginProtocolScheme: envProperties['facebookLoginProtocolScheme']
        ]

        resValue 'string', 'facebookAppId', envProperties['facebookAppId']
        resValue 'string', 'app_name', envProperties['appName']
    }

    signingConfigs {
        release {
            keyAlias envProperties['keyAlias']
            keyPassword envProperties['keyPassword']
            storeFile rootProject.file('../configs/' + envProperties['storeFile'])
            storePassword envProperties['storePassword']
        }
    }

    buildTypes {
        release {
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }

        debug {
            signingConfig signingConfigs.release
        }
    }

}

flutter {
    source '../..'
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test:runner:1.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.1.1'
    implementation platform("com.google.firebase:firebase-bom:32.8.1")

    implementation 'com.google.android.material:material:1.6.0'
    implementation 'com.android.support:multidex:1.0.3'
    implementation 'androidx.browser:browser:1.3.0'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.4.0'
    implementation("com.google.firebase:firebase-auth")
    implementation("com.google.android.gms:play-services-auth:21.1.0")
    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:2.0.4'


    // Uncomment if needed:
    // implementation 'com.onesignal:OneSignal:[5.0.0, 5.99.99]'
}

googleServices {
    disableVersionCheck = true
}

// === Copy google-services.json to both debug & release ===
tasks.register("copyGoogleServices", Copy) {
    from("${rootProject.projectDir}/configs")
    include "google-services.json"
    into("${projectDir}/src/debug")
    doLast {
        copy {
            from("${rootProject.projectDir}/configs/google-services.json")
            into("${projectDir}/src/release")
        }
    }
    outputs.files(
            file("${projectDir}/src/debug/google-services.json"),
            file("${projectDir}/src/release/google-services.json")
    )
}

// === Copy custom config files to root project directory ===
tasks.register("copyConfigFiles", Copy) {
    from("${rootProject.projectDir}/configs/customized")
    include "**"
    into(rootProject.projectDir)
    outputs.dir(rootProject.projectDir)
}

afterEvaluate {
    // Google Services: Ensure both debug and release processing waits
    ["processDebugGoogleServices", "processReleaseGoogleServices"].each { taskName ->
        tasks.matching { it.name == taskName }.configureEach {
            dependsOn("copyGoogleServices", "copyConfigFiles")
        }
    }

    // Flutter build tasks: Ensure both debug and release builds wait
    ["compileFlutterBuildDebug", "compileFlutterBuildRelease"].each { taskName ->
        tasks.matching { it.name == taskName }.configureEach {
            dependsOn("copyGoogleServices", "copyConfigFiles")
        }
    }

    // Asset-related tasks: All debug/release asset packaging waits for config
    tasks.matching { it.name.contains("package") || it.name.contains("merge") }.configureEach {
        dependsOn("copyConfigFiles")
    }
}



println "ðŸ”§ Copying configs/google-services.json to android/app/google-services.json"
preBuild.dependsOn(copyGoogleServices)

println "ðŸ”§ Copying configs/customized to project..."
println "\nðŸª„  Building " + envProperties['appName'] + "... ðŸª„\n"
println "\nðŸ”‘ Signing with keystore " + envProperties['storeFile'] + "... ðŸ”‘\n"
preBuild.dependsOn(copyConfigFiles)
